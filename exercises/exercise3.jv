pipeline countrystatsPipline {

    FileDownloader
        -> countrystatsText2XLSXInterpreter
        -> countrystatsSheetpicker
        -> countrystatsRenameHeadersP2
        -> countrystatsRenameHeadersRS
        -> countrystatsSelectDataRange
        -> BondIssuanceTableMaker
        -> BondIssuanceTableSave;
    countrystatsSelectDataRange
        ->GdpPerCapitaTableMaker
        ->GdpPerCapitaTableSave;

    block FileDownloader oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block countrystatsText2XLSXInterpreter oftype XLSXInterpreter { }
    block countrystatsSelectDataRange oftype CellRangeSelector {
        select: range P2:S45;
    }
    block countrystatsSheetpicker oftype SheetPicker {
        sheetName: 'Figure S5.1.2';
    }
    block countrystatsRenameHeadersP2 oftype CellWriter {
        at: range P2:P2;
        write: [
            "Country Code",
        ];
    }
    block countrystatsRenameHeadersRS oftype CellWriter {
        at:  range R2:S2;
        write:  [
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }
    valuetype ValidatedGdp oftype decimal {
        constraints: [
            CheckerGdp
        ];
    }
    constraint CheckerGdp on decimal: value>0;
    valuetype ValidatedBond oftype decimal {
        constraints: [
            CheckerBond
        ];
    }
    constraint CheckerBond on decimal: value >= 0 and value <= 1;
    block BondIssuanceTableMaker oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype ValidatedBond
        ];
    }
    block GdpPerCapitaTableMaker oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype ValidatedGdp
        ];
    }
    block BondIssuanceTableSave oftype SQLiteLoader {

        table: "bondIssuance";
        file: "./country-stats.sqlite";
    }
    block GdpPerCapitaTableSave oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
    }

}